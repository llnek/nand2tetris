

options {
  NODE_DEFAULT_VOID = true;
  NODE_CLASS="ASTNode";
  NODE_PREFIX = "";
  MULTI=true;
  LOOKAHEAD=2;
  STATIC=false;
}

PARSER_BEGIN(ASTGentor)
package czlab.tecs.p11;

import java.util.ArrayList;
import java.util.Stack;
import java.util.List;

public class ASTGentor {

  public static int RIGHT= 1;
  public static int LEFT= -1;

  public SimpleNode parseOneUnit()
  throws Exception {
    return parseClazz();
  }

  public static Object speek(Stack<Object> s) {
    return s.empty() ? null : s.peek();
  }

  public static void evalOP(
    ASTNode jjt, List<Object> out, Stack<Object> stk) {
    int bind= (int) jjt.nested.get("bind");
    int rank= (int) jjt.nested.get("rank");
    ASTNode top= (ASTNode)speek(stk);
    while (top != null) {
      int r = (int)top.nested.get("rank");
      if (rank < r ||
          ((r == rank) && (bind == LEFT))) {
        out.add(stk.pop());
        top=(ASTNode)speek(stk);
      } else {
        break;
      }
    }
    stk.push(jjt);
  }

  public static void cfgOP(
    ASTNode jjt, Token t, int bind, int rank) {

    jjt.nested.put("bind", bind);
    jjt.nested.put("rank", rank);
    jjt.value=t.image;
  }
}

PARSER_END(ASTGentor)

SKIP :
{
  < " " >
  | < "\t" >
  | < "\n" >
  | < "\r" >
  | < "//" (~["\n"])* "\n" >
  | <"/*" (~["*"])* "*" (~["/"] (~["*"])* "*")* "/">
}


/**********************************************
 * THE Jack TOKEN SPECIFICATION STARTS HERE *
 **********************************************/

TOKEN :
{
  < CLASS: "class" >
  | < CONSTRUCTOR: "constructor" >
  | < FUNCTION: "function" >
  | < METHOD: "method" >
  | < FIELD: "field" >
  | < STATIK: "static" >
  | < VAR: "var" >
  | < INT: "int" >
  | < CHAR: "char" >
  | < BOOLEAN: "boolean" >
  | < VOID: "void" >
  | < TRUE: "true" >
  | < FALSE: "false" >
  | < NULL: "null" >
  | < THIS: "this" >
  | < LET: "let" >
  | < DO: "do" >
  | < IF: "if" >
  | < ELSE: "else" >
  | < WHILE: "while" >
  | < RETURN: "return" >
  | < LCURLY: "{" >
  | < RCURLY: "}" >
  | < LPAREN: "(" >
  | < RPAREN: ")" >
  | < LBRAC: "[" >
  | < RBRAC: "]" >
  | < DOT: "." >
  | < COMMA: "," >
  | < SEMICOLON: ";" >
  | < PLUS: "+" >
  | < MINUS: "-" >
  | < STAR: "*" >
  | < SLASH: "/" >
  | < AND: "&" >
  | < BAR: "|" >
  | < LT: "<" >
  | < GT: ">" >
  | < EQ: "=" >
  | < TILDA: "~" >

}

TOKEN :
{
  < QSTRING: "\"" (~["\"", "\\"] | "\\\"" | "\\\\")* "\"" >
  |
  < NUMBER: "0" | ["1" - "9"] (["0" - "9"])* >
}

TOKEN :
{
  < IDENTIFIER: <ALPHA> (<ALPHA>|<DIGIT>)* >
  |
  < #ALPHA: "_" | ["a"-"z", "A"-"Z"]>
  |
  < #DIGIT: ["0"-"9"]>
}

TOKEN :
{
  < SCOPEDINVOKER: <IDENTIFIER> <DOT> <IDENTIFIER> >
}

/************************************************
 * THE Jack GRAMMAR SPECIFICATION STARTS HERE *
 ************************************************/
//kenl
ClassDec parseClazz() #ClassDec :
{Token z;}
{
  <CLASS> z=className() <LCURLY>
  (classVarDec())*
  (subroutineDec())*
  <RCURLY> {
    jjtThis.props.put("name", z.image);
    return jjtThis;
  }
}

//////////////////////////////////////////////////////////////////////////////
//
void classVarDec() #ClassVarDec :
{List<String> vs= new ArrayList<String>();
 Token v, t;
 String q="";}
{
  [<STATIK> {q=token.image;} |
   <FIELD> {q=token.image;}]
  t=type()
  v=varName()
  { vs.add(v.image); }
  (<COMMA> v=varName()
  { vs.add(v.image); })* <SEMICOLON> {
    jjtThis.props.put("qualifier", q);
    jjtThis.props.put("type", t.image);
    jjtThis.props.put("vars", vs);
  }
}

//;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
//
Token type() :
{}
{
  <INT>  { return token;} |
  <CHAR>  { return token;} |
  <BOOLEAN> { return token;} |
  //classNAme()
  <IDENTIFIER> { return token;}
}

//;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
//
void subroutineDec() #SubroutineDec :
{String r="", q="";
 Object vs=null;
 Object pml=null;
 ASTNode ss;
 Token sn,t;}
{
  [<CONSTRUCTOR> {q=token.image;} |
   <FUNCTION> {q=token.image;} |
   <METHOD> {q=token.image;}]
  [<VOID> {r=token.image;} |
   t=type() {r=t.image;}]
  sn=subroutineName()
  <LPAREN>
  pml=parameterList()
  <RPAREN>
  <LCURLY> (vs=varDec())* statements() <RCURLY> {
    jjtThis.props.put("qualifier", q);
    jjtThis.props.put("type", r);
    jjtThis.props.put("name", sn);
    if (pml != null) {
      jjtThis.props.put("args", pml);
    }
    if (vs != null) {
      jjtThis.props.put("vars", vs);
    }
    ss=(ASTNode)jjtThis.popChild();
    jjtThis.nested.put("statements",ss.popAll());
  }
}

//;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
//
Object parameterList() :
{List<String> ps= new ArrayList<String>();
 Token vn, t;}
{
  ((t=type() vn=varName() {
    ps.add(t.image); ps.add(vn.image);
    })
   (<COMMA> t=type() vn=varName() {
    ps.add(t.image); ps.add(vn.image);
    })*)? {
    return ps;
  }
}

//;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
//
Object varDec() :
{List<String> ps=new ArrayList<String>();
Token vn, t;}
{
  <VAR> t=type() vn=varName() {
    ps.add(t.image); ps.add(vn.image);
  }
  (<COMMA> vn=varName() {
    ps.add(t.image); ps.add(vn.image);
   })* <SEMICOLON> {
    return ps;
  }
}

//;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
//
void statements() #Statements :
{}
{
  (statement())*
}

//;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
//
void statement() :
{}
{
  letStatement()  |
  ifStatement()  |
  whileStatement()  |
  doStatement()  |
  returnStatement()
}

//;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
//
void letStatement() #LetStatement :
{Token vn;}
{
  <LET> vn=varName()
  (<LBRAC> expr(vn.image+"[]") <RBRAC>)?
  <EQ> expr("let to " + vn.image) <SEMICOLON> {
    jjtThis.nested.put("varr", vn.image);
    int cnt= jjtThis.jjtGetNumChildren();
    if (cnt == 2) {
      jjtThis.nested.put("lhs", jjtThis.jjtGetChild(0));
      jjtThis.nested.put("rhs", jjtThis.jjtGetChild(1));
    } else {
      jjtThis.nested.put("rhs", jjtThis.jjtGetChild(0));
    }
    jjtThis.removeChildren();
  }
}

//;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
//
void ifStatement() #IfStatement :
{ASTNode n;}
{
  <IF> <LPAREN> expr("if test") <RPAREN>
  <LCURLY> statements() <RCURLY> #IfBlock(1)
  (<ELSE> <LCURLY> statements() <RCURLY> #ElseBlock(1))? {
    jjtThis.nested.put("test", jjtThis.popChild());
    n= (ASTNode)jjtThis.popChild(); //if
    jjtThis.nested.put("then", n.popChild().popAll());
    n= (ASTNode)jjtThis.popChild(); //else?
    if (n != null) {
      jjtThis.nested.put("else", n.popChild().popAll());
    }
  }
}

//;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
//
void whileStatement() #WhileStatement :
{}
{
  <WHILE> <LPAREN> expr("while-test") <RPAREN>
    <LCURLY> statements() <RCURLY> #WhileBlock(1) {
    jjtThis.nested.put("test", jjtThis.popChild());
    jjtThis.nested.put("body", jjtThis.popChild().popChild().popAll());
  }
}

//;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
//
void doStatement() #DoStatement :
{}
{
  <DO> subroutineCall() <SEMICOLON> {
    jjtThis.nested.put("call", jjtThis.popChild());
  }
}

//;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
//
void returnStatement() #ReturnStatement :
{}
{
  <RETURN> <SEMICOLON> |
  <RETURN> expr("return value") <SEMICOLON> {
    jjtThis.nested.put("value", jjtThis.popChild());
  }
}

//;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
//
void expr(String desc) #Expression :
{List<Object> out=new ArrayList<Object>();
 Stack<Object> stk=new Stack<Object>(); }
{
  //apply algo resulting in postfix
  term(out, stk) (op(out, stk) term(out,stk))* {
    //add back all remaining operators
    for (Object obj=speek(stk); obj != null; ) {
      out.add(stk.pop());
      obj=speek(stk);
    }
    jjtThis.nested.put("output", out);
  }
}

//;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
//
void term(List<Object> out, Stack<Object> stk) #Term :
{Object e=null;
 String s;
 Token t, vn;}
{
  <LPAREN> expr("term-group") <RPAREN> {
    jjtThis.nested.put("group", jjtThis.popChild());
    out.add(jjtThis);
  } |
  t=unaryOp() term(out, stk) {
    jjtThis.nested.put("unary", t.image);
    jjtThis.nested.put("term", jjtThis.popChild());
    out.add(jjtThis);
  } |
  t= integerConstant() {
    jjtThis.nested.put("literal", "int");
    jjtThis.value=t.image;
    out.add(jjtThis);
  } |
  s=stringConstant() {
    jjtThis.nested.put("literal", "String");
    jjtThis.value=s;
    out.add(jjtThis);
  }  |
  t=keywordConstant() {
    jjtThis.nested.put("literal", "keyword");
    jjtThis.value=t.image;
    out.add(jjtThis);
  } |
  subroutineCall() {
    jjtThis.nested.put("call", jjtThis.popChild());
    out.add(jjtThis);
  } |
  vn=varName() <LBRAC> expr(vn.image+"[]") <RBRAC>  {
    jjtThis.nested.put("index", jjtThis.popChild());
    jjtThis.nested.put("varr", vn.image);
    out.add(jjtThis);
  } | vn=varName() {
    jjtThis.nested.put("varr", vn.image);
    out.add(jjtThis);
  }
}

//;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
//
void subroutineCall() #SubroutineCall :
{Token sn;}
{
  sn=subroutineName()
    <LPAREN> exprList("caller: " + sn.image) <RPAREN> {
    jjtThis.nested.put("target", sn.image);
    jjtThis.nested.put("params", jjtThis.popChild().popAll());
  } |
  sn=scopedCall()
  <LPAREN> exprList("caller: " + sn.image) <RPAREN> {
    jjtThis.nested.put("target", sn.image);
    jjtThis.nested.put("params", jjtThis.popChild().popAll());
  }
}

//;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
//
void exprList(String desc) #ExpressionList :
{}
{
  (expr(desc) (<COMMA> expr(desc) )*)?
}

//;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
//
Token scopedCall() :
{}
{
  <SCOPEDINVOKER> {
    //System.out.println("scopedinvoker= " + t.image);
    //return t.image.split("\\.");
    return token;
  }
}

//;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
//
void op(List<Object> out, Stack<Object> stk) #OP :
{}
{
  //precedence top to bottom
  <BAR> {
    ASTGentor.cfgOP(jjtThis, token, -1, 13);
    ASTGentor.evalOP(jjtThis, out,stk);
    } |
  <AND> {
    ASTGentor.cfgOP(jjtThis, token, -1, 12);
    ASTGentor.evalOP(jjtThis, out,stk);
    } |
  <EQ> {
    ASTGentor.cfgOP(jjtThis, token, -1, 8);
    ASTGentor.evalOP(jjtThis, out,stk);
    } |
  <LT> {
    ASTGentor.cfgOP(jjtThis, token, -1, 7);
    ASTGentor.evalOP(jjtThis, out,stk);
    } |
  <GT> {
    ASTGentor.cfgOP(jjtThis, token, -1, 7);
    ASTGentor.evalOP(jjtThis, out,stk);
    } |
  <STAR> {
    ASTGentor.cfgOP(jjtThis, token, -1, 4);
    ASTGentor.evalOP(jjtThis, out,stk);
    } |
  <SLASH> {
    ASTGentor.cfgOP(jjtThis, token, -1, 4);
    ASTGentor.evalOP(jjtThis, out,stk);
    } |
  <PLUS> {
    ASTGentor.cfgOP(jjtThis, token, 1, 2);
    ASTGentor.evalOP(jjtThis, out,stk);
    } |
  <MINUS> {
    ASTGentor.cfgOP(jjtThis, token, 1, 2);
    ASTGentor.evalOP(jjtThis, out,stk); }
}

//;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
//
Token unaryOp() :
{}
{
  <MINUS> {return token;} |
  <TILDA> {return token;}
}

//;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
//
Token integerConstant() :
{}
{ <NUMBER> { return token;} }

//;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
//
String stringConstant() :
{String s;}
{
  <QSTRING> {
    s=token.image;
    return s.substring(1,s.length()-1);
  }
}

//;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
//
Token keywordConstant() :
{}
{
  <TRUE> { return token; } |
  <FALSE> { return token; } |
  <NULL> { return token; } |
  <THIS> { return token; }
}

//;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
//
Token subroutineName() :
{}
{
  <IDENTIFIER> { return token;}
}

//;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
//
Token className() :
{}
{
  <IDENTIFIER> {return token;}
}

//;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
//
Token varName() :
{}
{
  <IDENTIFIER> { return token;}
}

//;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
//EOF



