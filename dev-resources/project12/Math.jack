// This file is part of the materials accompanying the book
// "The Elements of Computing Systems" by Nisan and Schocken,
// MIT Press. Book site: www.idc.ac.il/tecs
// File name: projects/12/Math.jack

/**
 * A basic math library.
 */
class Math {

  static Array bmask, wmask;

  /** Initializes the library. */
  function void init() {

    let bmask=Array.new(16);
    let wmask=Array.new(16);
    let bmask[0]= 1;
    let bmask[1]= 2;
    let bmask[2]= 4;
    let bmask[3]= 8;
    let bmask[4]= 16;
    let bmask[5]= 32;
    let bmask[6]= 64;
    let bmask[7]= 128;
    let bmask[8]= 256;
    let bmask[9]= 512;
    let bmask[10]= 1024;
    let bmask[11]= 2048;
    let bmask[12]= 4096;
    let bmask[13]= 8192;
    let bmask[14]= 16384;
    let bmask[15]= -32767 - 1;

    let wmask[0]= -2;
    let wmask[1]= -3;
    let wmask[2]= -5;
    let wmask[3]= -9;
    let wmask[4]= -17;
    let wmask[5]= -33;
    let wmask[6]= -65;
    let wmask[7]= -129;
    let wmask[8]= -257;
    let wmask[9]= -513;
    let wmask[10]= -1025;
    let wmask[11]= -2049;
    let wmask[12]= -4097;
    let wmask[13]= -8193;
    let wmask[14]= -16385;
    let wmask[15]= 32767;

    return;
  }

  function int bitoff(int bit) {
    if ((bit < 0) | (bit > 15)) {
      do Sys.error(15);
    }
    return wmask[bit];
  }

  function int biton(int bit) {
    if ((bit < 0) | (bit > 15)) {
      do Sys.error(15);
    }
    return bmask[bit];
  }

  /** Returns the absolute value of x. */
  function int abs(int x) {
    if (x < 0) {
      return - x;
    } else {
      return x;
    }
  }

  function int multiply(int x, int y) {
    var int sign, sum, sx, i, b;
    if ((x = 0) | (y = 0)) {
      return 0;
    }
    let sign=0;
    if (x < 0) {
      let sign = sign + 1;
      let x= Math.abs(x);
    }
    if (y < 0) {
      let sign = sign + 1;
      let y= Math.abs(y);
    }
    if (y > x) {
      //swap to optimize
      let b=y;
      let y=x;
      let x=b;
    }
    let sum=0;
    let sx=x;
    let i=0;
    while (i < 16) {
      let b= Math.biton(i);
      if (b = (b & y)) {
        let sum=sum+sx;
      }
      let sx=sx+sx;
      let i=i+1;
    }

    if (sign = 1) {
      return - sum;
    } else {
      return sum;
    }
  }

  /** Returns the product of x and y. */
  function int multiplySlow(int x, int y) {
    var int tmp,sign,cnt,sum;
    if ((x = 0) | (y = 0)) {
      return 0;
    }

    let sign=0;
    let sum=0;
    let cnt=0;
    if (x < 0) {
      let sign = sign + 1;
      let x= Math.abs(x);
    }
    if (y < 0) {
      let sign = sign + 1;
      let y= Math.abs(y);
    }

    if (y > x) {
      //swap to optimize
      let tmp=y;
      let y=x;
      let x=tmp;
    }

    while (cnt < y) {
      let sum = sum + x;
      let cnt = cnt +1;
    }

    if (sign = 1) {
      return - sum;
    } else {
      return sum;
    }
  }

  /** Returns the integer part of x/y. */
  function int divide(int x, int y) {
    var int sign,cnt;
    var boolean stop;

    if (y = 0) {
      do Sys.error(3);
    }

    if (x = 0) {
      return 0;
    }

    let stop=false;
    let cnt=0;
    let sign=0;

    if (x < 0) {
      let sign = sign + 1;
      let x= Math.abs(x);
    }
    if (y < 0) {
      let sign = sign + 1;
      let y= Math.abs(y);
    }

    while (~stop) {
      let x= x - y;
      if (x < 0) {
        let stop=true;
      } else {
        let cnt = cnt + 1;
        if (x = 0) {
          let stop=true;
        }
      }
    }

    if (sign = 1) {
      if (cnt = 0) { return 0; } else {
        return - cnt;
      }
    } else {
      return cnt;
    }
  }

  /** Returns the integer part of the square root of x. */
  function int sqrt(int x) {
    //y^2 = x, find minimum y
    var boolean stop;
    var int y2, y;

    if ((x = 1) | (x = 0)) { return x; }
    if (x < 0) { do Sys.error(4); }

    let stop=false;
    let y=1;
    while (~ stop) {
      let y2= Math.multiply(y,y);
      if ((y2 < 0) | (y2 > x)) {
        // y2 value < 0 => int maxed out, stop!
        let stop= true;
        let y= y-1;
      } else {
        let y=y+1;
      }
    }

    return y;
  }

  /** Returns the greater number. */
  function int max(int a, int b) {
    if (a > b) {
      return a;
    } else {
      return b;
    }
  }

  /** Returns the smaller number. */
  function int min(int a, int b) {
    if (a < b) {
      return a;
    } else {
      return b;
    }
  }
}

